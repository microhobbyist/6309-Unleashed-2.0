Name     SystemDecode ;
PartNo   ATF22V10C ;
Date     2025-09-25 ;
Revision v4.1 ;
Designer Frederic Segard ;
Company  MicroHobbyist ;
Assembly None ;
Location None ;
Device   g22v10 ;

/* ==============================================
   System I/O Decode and MRDY Clock-Stretch Logic
   ============================================== */

/* WinCUPL Options:
      General: Deactivate Unused OR Terms, One-hot-bit State Machine
      Minimization: Presto
      Optimization: Keep XOR Equations
*/

/* =========================================================
   LEGEND / FILE MAP
   ---------------------------------------------------------
   1. PIN ASSIGNMENTS AND SYSTEM OVERVIEW
   2. INPUT/OUTPUT DECLARATIONS
   3. ADDRESS DECODE EQUATIONS (I/O Chip Selects)
   4. END OF FILE
   ========================================================= */

/* =========================================================
   1. PIN ASSIGNMENTS AND SYSTEM OVERVIEW
   ---------------------------------------------------------
        E  i---|1     24|---p  Vcc
    !IORQ  i---|2     23|---o  !ROMdis
       RW  i---|3     22|---o  !SPDreg
       A7  i---|4     21|---o  !IRQreg
       A6  i---|5     20|---o  !FIRQreg
       A5  i---|6     19|---o  !STATreg
       A4  i---|7     18|---o  !RTCcs
       A3  i---|8     17|---o   RTCas
       A2  i---|9     16|---o   RTCds
       A1  i---|10    15|---o  !ACIA1cs
       A0  i---|11    14|---o  !ACIA2cs
      GND  p---|12    13|---i  GND
   ---------------------------------------------------------
   CLKin and E are both clocks; all other inputs are 
   for I/O decoding or MRDY/clock stretching support.
   ========================================================= */

/* =========================================================
   2. INPUT/OUTPUT DECLARATIONS
   ========================================================= */

/* ---- Inputs ---- */
PIN 1  = E     ;     /* CPU E clock (for decode qualification) */
PIN 2  = !IORQ ;     /* (from PLD 1) I/O request, active-low */
PIN 3  = RW    ;     /* Read/Write line */
PIN 4  = A7    ;     /* Address bit 7 */
PIN 5  = A6    ;     /* Address bit 6 */
PIN 6  = A5    ;     /* Address bit 5 */
PIN 7  = A4    ;     /* Address bit 4 */
PIN 8  = A3    ;     /* Address bit 3 */
PIN 9  = A2    ;     /* Address bit 2 */
PIN 10 = A1    ;     /* Address bit 1 */
PIN 11 = A0    ;     /* Address bit 0 */

/* ---- Outputs ---- */
PIN 23 = !ROMdis  ;  /* ROM disable (active-low) */
PIN 22 = !SPDreg  ;  /* Speed register (active-low) */
PIN 21 = !IRQreg  ;  /* Interrupt register (active-low) */
PIN 20 = !FIRQreg ;  /* Fast Interrupt register (active-low) */
PIN 19 = !STATreg ;  /* Status Register */
PIN 18 = !RTCcs   ;  /* RTC chip select (active-low) */
PIN 17 =  RTCas   ;  /* RTC address strobe */
PIN 16 =  RTCds   ;  /* RTC data strobe */
PIN 15 = !ACIA1cs ;  /* ACIA1 serial port chip select (active-low) */
PIN 14 = !ACIA2cs ;  /* ACIA2 serial port chip select (active-low) */
           
/* ======================================================
   3. ADDRESS DECODE EQUATIONS (I/O CHIP SELECTS)
   ------------------------------------------------------
   All chip selects are active only when IORQ (from PLD 2) is
   low, E is high, and the address matches the target.
   ====================================================== */

FIELD ADDR = [A7..A0] ;  /* (shortcut) for decoding 8-bit address space */

!ROMdis  = ADDR:[00] & !RW & IORQ & E ;  /* ROM disable (Write) */
!SPDreg  = ADDR:[01]       & IORQ & E ;  /* Speed register */
!IRQreg  = ADDR:[02]       & IORQ & E ;  /* IRQ Priority Interrupt register */
!FIRQreg = ADDR:[03]       & IORQ & E ;  /* FIRQ Priority Interrupt register */
!STATreg = ADDR:[04] &  RW & IORQ & E ;  /* Status register (Read) */
!RTCcs   = ADDR:[05..06]   & IORQ & E ;  /* RTC chip select */
RTCas    = ADDR:[05]       & IORQ & E ;  /* RTC address strobe */
RTCds    = ADDR:[06]       & IORQ & E ;  /* RTC data strobe */
!ACIA1cs = ADDR:[08..0B]   & IORQ & E ;  /* ACIA1 serial port */
!ACIA2cs = ADDR:[0C..0F]   & IORQ & E ;  /* ACIA1 serial port */

/* ==========================================================
   4. END OF FILE
   ========================================================== */
