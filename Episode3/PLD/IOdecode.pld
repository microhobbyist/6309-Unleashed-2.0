Name     IODecode ;
PartNo   ATF750CL ;
Date     2025/12/10 ;
Revision 11 ;
Designer Frederic Segard ;
Company  MicroHobbyist ;
Assembly None ;
Location None ;
Device   v750c ;

/* -------- Inputs -------- */
PIN 1  =  CLK4x ;    /* To trigger WS OE */
PIN 2  = !IORQ ;     /* I/O Request, Active-low during I/O cycle */
PIN 3  =  RW ;       /* Read-Write line */
PIN 4  =  A0 ;       /* Address bit 0 - LSB */
PIN 5  =  A1 ;
PIN 6  =  A2 ;
PIN 7  =  A3 ;
PIN 8  =  A4 ;
PIN 9  =  A5 ;
PIN 10 =  A6 ;
PIN 11 =  A7 ;       /* Address bit 7 - MSB of I/O window */
/*  12 =  GND ;      /* Ground connection */
PIN 13 =  S0 ;       /* Current speed of CPU */
PIN 14 =  S1 ;       /* Current speed of CPU */

/* -------- Outputs -------- */
/*  24 =  VCC ;      /* +5V connection */
PIN 23 = !TIMERcs ;  /* Timer (HD63B40) */
PIN 22 = !CFcs ;     /* Compact Flash card (Recommend speed) */
PIN 21 = !ACIA1cs ;  /* ACIA (R65C52P3) */
PIN 20 = !RTCcs ;    /* Real-Time-Clock (DS12C887A) */
PIN 19 =  RTCas ;    /* Real-Time-Clock address strobe */
PIN 18 =  RTCds ;    /* Real-Time-Clock data strobe */
PIN 17 = !POSTcs ;   /* Power-On-Self-Test (MC14495) */           /*  */
PIN 16 =  WS0 ;      /* Waitstate LSB bus(Tristate, pulled low) */
PIN 15 =  WS1 ;      /* Waitstate MSB bus(Tristate, pulled low) */

PINNODE 30 = WS_EN ;

FIELD ADDR = [A7..A0] ;

TIMERcs = ADDR:[28..2F] & IORQ ;  /* HD63B40 Programmable Timer */
CFcs    = ADDR:[08..0F] & IORQ ;  /* Compact Flash card */
ACIA1cs = ADDR:[60..67] & IORQ ;  /* Dual ACIA R65C52P3 */
RTCcs   = ADDR:[26..27] & IORQ ;  /* DS12C887A Real-Time Clock */
RTCas   = ADDR:[26]     & IORQ ;  /* RTC Address Strobe */
RTCds   = ADDR:[27]     & IORQ ;  /* RTC Data Strobe */
POSTcs  = ADDR:[04]     & IORQ ;  /* MC14495 Hex display POST */

/* =========================================================
   WAIT-STATE LOGIC (20MHz osc, max E = 5MHz)
   ---------------------------------------------------------
   Model: t_bus ~ (1 + WS) * T_E

   For E clock:
     Turtle: S1:S0=00, E=0.625MHz  - T_E = 1600ns
     Slow:   S1:S0=01, E=1.25MHz   - T_E = 800ns
     Fast:   S1:S0=10, E=2.5MHz    - T_E = 400ns
     Turbo:  S1:S0=11, E=5MHz      - T_E = 200ns

   Device "needs" (approx worst-case):
     TIMER (HD63B40)       : tACC ~ 500ns
     CF (PIO Mode 0)       : t_cycle >= 600ns
     ACIA (R65C52P3)       : t_bus < 300ns
     RTC (DS12C887A)       : t_bus < 200ns
     POST (MC14495)        : t_bus < 300ns
   ========================================================= */
FIELD CS = [TIMERcs, CFcs, ACIA1cs, RTCcs, POSTcs, S1, S0] ;
FIELD WS = [WS1,WS0] ;
TABLE CS => WS {

/* ---------------------------------------------------------
   Turtle mode: S1:S0 = 00
   E = 0.625MHz, T_E = 1600ns
   WS=0 -> t_bus = 1*1600ns, which >> all requirements.
   --------------------------------------------------------- */
'b'0000000 => 'b'00 ;    /* Idle:   WS=0 - 1600ns, no device */
'b'1000000 => 'b'00 ;    /* TIMER:  WS=0 - 500ns (HD63B40) */
'b'0100000 => 'b'00 ;    /* CF:     WS=0 - 600ns (PIO0) */
'b'0010000 => 'b'00 ;    /* ACIA:   WS=0 - 300ns (R65C52P3) */
'b'0001000 => 'b'00 ;    /* RTC:    WS=0 - 200ns (DS12C887A) */
'b'0000100 => 'b'00 ;    /* POST:   WS=0 - 300ns (MC14495) */

/* ---------------------------------------------------------
   Slow mode: S1:S0 = 01
   E = 1.25MHz, T_E = 800ns
   WS=0 -> t_bus = 1*800ns, still >= all device needs.
   --------------------------------------------------------- */
'b'0000001 => 'b'00 ;    /* Idle:   WS=0 - 800ns, no device */
'b'1000001 => 'b'00 ;    /* TIMER:  WS=0 - 500ns */
'b'0100001 => 'b'00 ;    /* CF:     WS=0 - 600ns */
'b'0010001 => 'b'00 ;    /* ACIA:   WS=0 - 300ns */
'b'0001001 => 'b'00 ;    /* RTC:    WS=0 - 200ns */
'b'0000101 => 'b'00 ;    /* POST:   WS=0 - 300ns */

/* ---------------------------------------------------------
   Fast mode: S1:S0 = 10
   E = 2.5MHz, T_E = 400ns
   WS=0 -> t_bus = 1*400ns:
     TIMER/CF need > 400ns, so give them 1 WS.
   --------------------------------------------------------- */
'b'0000010 => 'b'00 ;    /* Idle:   WS=0 - 400ns, no device */
'b'1000010 => 'b'01 ;    /* TIMER:  WS=1 - 500ns */
'b'0100010 => 'b'01 ;    /* CF:     WS=1 - 600ns */
'b'0010010 => 'b'00 ;    /* ACIA:   WS=0 - 300ns */
'b'0001010 => 'b'00 ;    /* RTC:    WS=0 - 200ns */
'b'0000110 => 'b'00 ;    /* POST:   WS=0 - 300ns */

/* ---------------------------------------------------------
   Turbo mode: S1:S0 = 11
   E = 5MHz, T_E = 200ns
   WS=0 -> t_bus = 1*200ns.
   We size WS so (1+WS)*200ns meets each device's need.
   --------------------------------------------------------- */
'b'0000011 => 'b'00 ;    /* Idle:   WS=0 - 200ns, no device */
'b'1000011 => 'b'10 ;    /* TIMER:  WS=2 - 500ns */
'b'0100011 => 'b'10 ;    /* CF:     WS=2 - 600ns */
'b'0010011 => 'b'01 ;    /* ACIA:   WS=1 - 300ns */
'b'0001011 => 'b'01 ;    /* RTC:   *WS=1 - ~200ns */
'b'0000111 => 'b'01 ;    /* POST:   WS=1 - 300ns */
}

/* Force WS_EN to be a valid output macrocell */
WS_EN.d  = WS0 # WS1 ;   /* WS non-zero = enable */
WS_EN.ck = CLK4x ;       /* Pseudo force combinational behavior */
WS_EN.sp = 'b'0 ;
WS_EN.ar = 'b'0 ;

/* OE controls */
WS0.oe = WS_EN ;
WS1.oe = WS_EN ;

TIMERcs.oe = 'b'1 ;
CFcs.oe    = 'b'1 ;
ACIA1cs.oe = 'b'1 ;
RTCcs.oe   = 'b'1 ;
POSTcs.oe  = 'b'1 ;
