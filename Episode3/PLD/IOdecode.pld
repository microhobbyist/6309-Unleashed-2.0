Name     IODecode ;
PartNo   ATF22V10C ;
Date     2025/11/17 ;
Revision 8 ;
Designer Frederic Segard ;
Company  MicroHobbyist ;
Assembly None ;
Location None ;
Device   g22v10 ;

PIN 1  = !IORQ ;     /* I/O Request, Active-low during I/O cycle */
PIN 2  =  A0 ;       /* Address bit 0 - LSB */
PIN 3  =  A1 ;
PIN 4  =  A2 ;
PIN 5  =  A3 ;
PIN 6  =  A4 ;
PIN 7  =  A5 ;
PIN 8  =  A6 ;
PIN 9  =  A7 ;       /* Address bit 7 - MSB of I/O window */
PIN 10 =  SPDR ;     /* The red LED indicates turbo speed (yellow is fast) */
PIN 11 =  SPDG ;     /* The green LED indicates normal speed (yellow is fast) */
/*  12 =  GND ;      /* Ground connection */
PIN 13 =  AnyCS ;    /* Pin 14 controls the output enable pin. It's wasteful, but
                        it's the only way I could tristate the WS bus to work. */

/*  24 =  VCC ;      /* +5V connection */
PIN 23 = !TIMERcs ;  /* $FF00-07, Timer (HD63B40) */
PIN 22 = !CFcs ;     /* $FF08-0F, Compact Flash card */
PIN 21 = !ACIAcs ;   /* $FF10-13, ACIA (R65C51R4) */
PIN 20 = !USBcs ;    /* $FF14-15  CH376S SD card and USB thumb-drive */
PIN 19 = !RTCcs ;    /* $FF16-17, Realtime clock (DS12C887A) */
PIN 18 =  RTCas ;    /* $FF16,    RTCs Address Strobe */
PIN 17 =  RTCds ;    /* $FF17,    RTCs Data Strobe */
PIN 16 =  WS0 ;      /* Waitstate LSB bus(Tristate, pulled low) */
PIN 15 =  WS1 ;      /* Waitstate MSB bus(Tristate, pulled low) */
PIN 14 =  IOCS ;     /* Wired to pin 13 (pulled high) */

FIELD ADDR = [A7..A0];
TIMERcs = ADDR:[00..07] & IORQ ;
CFcs    = ADDR:[08..0F] & IORQ ;
ACIAcs  = ADDR:[10..13] & IORQ ;
USBcs   = ADDR:[14..15] & IORQ ;
RTCas   = ADDR:[16]     & IORQ ;
RTCds   = ADDR:[17]     & IORQ ;
RTCcs   = RTCas # RTCds ;

IOCS = TIMERcs # ACIAcs # RTCcs # CFcs # USBcs ;

/* =========================================================
   WAIT-STATE LOGICgo
   cd plsgo
   ========================================================= */
FIELD CS = [TIMERcs, ACIAcs, RTCcs, CFcs, USBcs, SPDG, SPDR] ;
FIELD WS = [WS1,WS0] ;
TABLE CS => WS {
/* Slow mode (speed=00) - OFF .75MHz Max */
'b'0000000 => 'b'00 ;
'b'1000000 => 'b'00 ;
'b'0100000 => 'b'00 ;
'b'0010000 => 'b'00 ;
'b'0001000 => 'b'00 ;
'b'0000100 => 'b'00 ;
/* Normal mode (speed=10) - GREEN 1.5MHz Max */
'b'0000010 => 'b'00 ;
'b'1000010 => 'b'00 ;
'b'0100010 => 'b'00 ;
'b'0010010 => 'b'00 ;
'b'0001010 => 'b'01 ;
'b'0000110 => 'b'00 ;
/* Fast mode (speed=11) - YELLOW 3MHz Max */
'b'0000011 => 'b'00 ;
'b'1000011 => 'b'01 ;
'b'0100011 => 'b'01 ;
'b'0010011 => 'b'00 ;
'b'0001011 => 'b'10 ;
'b'0000111 => 'b'01 ;
/* Turbo mode (speed=01) - RED 6MHz Max */
'b'0000001 => 'b'00 ;
'b'1000001 => 'b'10 ;
'b'0100001 => 'b'01 ;
'b'0010001 => 'b'01 ;
'b'0001001 => 'b'11 ;
'b'0000101 => 'b'11 ;
}

WS0.oe = AnyCS ;
WS1.oe = AnyCS ;
