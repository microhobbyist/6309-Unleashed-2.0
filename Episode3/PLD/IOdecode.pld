Name     IODecode ;
PartNo   ATF750CL ;
Date     2025/11/17 ;
Revision 8 ;
Designer Frederic Segard ;
Company  MicroHobbyist ;
Assembly None ;
Location None ;
Device   v750c ;

/* -------- Inputs -------- */
PIN 1  =  E ;        /* E clock phase, used to qualify chip selects with IORQ */
PIN 2  = !IORQ ;     /* I/O Request, Active-low during I/O cycle */
PIN 3  =  RW ;       /* Read/Write */
PIN 4  =  A0 ;       /* Address bit 0 - LSB */
PIN 5  =  A1 ;
PIN 6  =  A2 ;
PIN 7  =  A3 ;
PIN 8  =  A4 ;
PIN 9  =  A5 ;
PIN 10 =  A6 ;
PIN 11 =  A7 ;       /* Address bit 7 - MSB of I/O window */
/*  12 =  GND ;      /* Ground connection */
PIN 13 =  SPDr ;     /* The red LED indicates turbo speed (yellow is fast) */
PIN 14 =  SPDg ;     /* The green LED indicates normal speed (yellow is fast) */

/* -------- Outputs -------- */
/*  24 =  VCC ;      /* +5V connection */
PIN 23 = !TIMERcs ;  /* Timer (HD63B40) */
PIN 22 = !CFcs ;     /* Compact Flash card */
PIN 21 = !ACIA1cs ;  /* ACIA (R65C51R4) */
PIN 20 = !POSTcs ;   /* Power-On-Self-Test (MC14495) */
PIN 19 = !INTvec ;   /* Priority Interrupt Controller */
PIN 18 = !ROMdis ;   /* ROM disable */
PIN 17 = !SPDsel ;   /* Speed select */
PIN 16 =  WS0 ;      /* Waitstate LSB bus(Tristate, pulled low) */
PIN 15 =  WS1 ;      /* Waitstate MSB bus(Tristate, pulled low) */

PINNODE 30 = WS_EN ;

VIA = IORQ & E ;    /* Valid I/O Address */
FIELD ADDR = [A7..A0] ;

TIMERcs = ADDR:[00..07]   & VIA ;
CFcs    = ADDR:[08..0F]   & VIA ;
ACIA1cs = ADDR:[10..13]   & VIA ;
POSTcs  = ADDR:[14]       & VIA ;
INTvec  = ADDR:[15] &  RW & VIA ;
ROMdis  = ADDR:[16] & !RW & VIA ;
SPDsel  = ADDR:[17]       & VIA ;

/* =========================================================
   WAIT-STATE LOGIC
   ========================================================= */
FIELD CS = [TIMERcs, CFcs, ACIA1cs, POSTcs, SPDg, SPDr] ;
FIELD WS = [WS1,WS0] ;
TABLE CS => WS {
/* Slow mode (speed=00) - OFF .75MHz Max */
'b'000000 => 'b'00 ;    /* Nothing */
'b'100000 => 'b'00 ;    /* TIMERcs */
'b'010000 => 'b'00 ;    /* CFcs */
'b'001000 => 'b'00 ;    /* ACIA1cs */
'b'000100 => 'b'00 ;    /* POSTcs */
/* Normal mode (speed=10) - GREEN 1.5MHz Max */
'b'000010 => 'b'00 ;    /* Nothing */
'b'100010 => 'b'00 ;    /* TIMERcs */
'b'010010 => 'b'01 ;    /* CFcs */
'b'001010 => 'b'00 ;    /* ACIA1cs */
'b'000110 => 'b'01 ;    /* POSTcs */
/* Fast mode (speed=11) - YELLOW 3MHz Max */
'b'000011 => 'b'00 ;    /* Nothing */
'b'100011 => 'b'01 ;    /* TIMERcs */
'b'010011 => 'b'10 ;    /* CFcs */
'b'001011 => 'b'00 ;    /* ACIA1cs */
'b'000111 => 'b'10 ;    /* POSTcs */
/* Turbo mode (speed=01) - RED 6MHz Max */
'b'000001 => 'b'00 ;    /* Nothing */
'b'100001 => 'b'10 ;    /* TIMERcs */
'b'010001 => 'b'11 ;    /* CFcs */
'b'001001 => 'b'01 ;    /* ACIA1cs */
'b'000101 => 'b'11 ;    /* POSTcs */
}

/* Force WS_EN to be a valid output macrocell */
WS_EN.d  = WS0 # WS1 ;   /* WS non-zero = enable */
WS_EN.ck = E ;           /* Pseudo force combinational behavior */
WS_EN.sp = 'b'0 ;
WS_EN.ar = 'b'0 ;

/* OE controls */
WS0.oe = WS_EN ;
WS1.oe = WS_EN ;