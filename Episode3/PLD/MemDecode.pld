Name     MEMDecode ;
PartNo   ATF22V10C ;
Date     2025/11/07 ;
Revision 5 ;
Designer Frederic Segard ;
Company  MicroHobbyist ;
Assembly None ;
Location None ;
Device   g22v10 ;

/* =========================================================
   Compiler Options
   ---------------------------------------------------------
   cupl -m2lxfjanb -u C:\Wincupl\Shared\Atmel.DL %1.PLD
   ========================================================= */

/* -------- Inputs -------- */
PIN 1  = E   ;  /* CPU E clock */
PIN 2  = A4  ;
PIN 3  = A5  ;
PIN 4  = A6  ;
PIN 5  = A7  ;
PIN 6  = A8  ;
PIN 7  = A9  ;
PIN 8  = A10 ;
PIN 9  = A11 ;
PIN 10 = A12 ;
PIN 11 = A13 ;
/*  12 = GND ;  /* Tie to Ground */
/*  13 = GND ;  /* Tie to Groung */
PIN 14 = A14 ;
PIN 15 = A15 ;

/* -------- Outputs (active-LOW) -------- */
/*  24 =  VCC        /* Tie to +5V */
PIN 23 = !CONST ;    /* MMU Constant Region window ($F000-$FFFF, 4KB) */
PIN 22 = !IORQ ;     /* I/O mapped area ($FF00-$FFEF, 240B) */
PIN 21 = !REG ;      /* Registers or future expansion ($FE00-$FEFF, 256B) */
PIN 20 = !PAGE ;     /* MMU Page/Task registers ($FC00-$FDFF, 512B) */
PIN 19 = !MREQ ;     /* All Memory area ($0000-$FBFF  63KB  */
                     /*             and  $FFF0-$FFFF  16B ) */
PIN 18 = !EXP ;      /* Future Expansion */
PIN 17 = !RAMaddr ;  /* Classical RAM Addressing (future option?) */
PIN 16 = !ROMaddr ;  /* Classical ROM Addressing (future option?) */


Fxxx = A15 & A14 & A13 & A12 ;
xFxx = A11 & A10 & A9  & A8  ;
xxFx =  A7 & A6  & A5  & A4  ;
FFFx = Fxxx &  xFxx &  xxFx  ;

/* $F000-$FFFF (4KB): I/O range */
BlockCONST =  Fxxx ;

/* $FF00-$FFEF (240B) : I/O range */
BlockIORQ =  Fxxx & xFxx & (!(A7 & A6 & A5 & A4)) ;

/* $FE00-$FEFF (256B) : Register range, or future expansion */
BlockREG =  Fxxx & (A11 & A10 & A9 & !A8) ;

/* $FC00-$FDFF (512B) : MMU Task/Page registers */
BlockPAGE =  Fxxx & A11 & A10 & !A9 ;

/* $0000-$FBFF + $FFF0-$FFFF (63KB): General memory area) */
BlockMEM = (!(Fxxx & A11 & A10)) # FFFx ;

BlockRAM = (!A15) # (Fxxx & !A11) # FFFx ;

BlockROM = A15 & !A12;

/* Output Equations (active-HIGH internal) */
CONST   = BlockCONST & E ;
IORQ    = BlockIORQ  & E ;
REG     = BlockREG   & E ;
PAGE    = BlockPAGE  & E ;
MREQ    = BlockMEM   & E ;
RAMaddr = BlockRAM   & E ;
ROMaddr = BlockROM   & E ;