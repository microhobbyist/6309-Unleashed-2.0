Name     MEMDecode ;
PartNo   ATF22V10C ;
Date     2025/12/10 ;
Revision 14 ;
Designer Frederic Segard ;
Company  MicroHobbyist ;
Assembly None ;
Location None ;
Device   g22v10 ;

/* -------- Inputs -------- */
PIN 1  = E ;         /* CPU Q clock */
PIN 2  = A2 ;
PIN 3  = A3 ;
PIN 4  = A4 ;
PIN 5  = A5 ;
PIN 6  = A6 ;
PIN 7  = A7 ;
PIN 8  = A8 ;
PIN 9  = A9 ;
PIN 10 = A10 ;
PIN 11 = A11 ;
/*  12 = GND ;       /* Tie to Ground */
PIN 13 = A12 ;
PIN 14 = A13 ;
PIN 15 = A14 ;
PIN 16 = A15 ;

/* -------- Outputs (active-LOW) -------- */
/*  24 =  VCC        /* Tie to +5V */
PIN 23 = !CBLK ;     /* MMU Constant Block Region window ($FE00-$FFFF) */
PIN 22 = !IORQ ;     /* I/O mapped area ($FF00-$FFEF) */
PIN 21 = !MREQ ;     /* All Memory area ($0000-$FDFF and $FFF0-$FFFF) */
PIN 20 =  ROMdis ;   /* ROM disable ($FFE0) */
PIN 19 = !SPDsel ;   /* Speed select ($FFE4) */
PIN 18 = !IRQvec ;   /* IRQ Priority Interrupt Controller */
PIN 17 = !FIRQvec ;  /* FIRQ Priority Interrupt Controller */

/* Intermediate product terms */
Fxxx = A15 & A14 & A13 & A12 ;
xFxx = A11 & A10 & A9  & A8 ;
xxFx = A7  & A6  & A5  & A4 ;
FFFx = Fxxx & xFxx & xxFx ;

/* $FE00-$FFFF (512B): I/O range + constant RAM range */
BlockCONST = Fxxx & A11 & A10 & A9 ;

/* $FF00-$FFEF (240B) : I/O range */
BlockIO = Fxxx & xFxx & (!(A7 & A6 & A5 & A4)) ;

/* $0000-$FDFF + $FFF0-$FFFF (63.5KB): General memory area) */
BlockMEM = (!Fxxx) # (Fxxx & !(A11 & A10 & A9)) # FFFx ;

/* Output Equations (active-HIGH internal) */
CBLK   = BlockCONST & E ;
IORQ   = BlockIO    & E ;
MREQ   = BlockMEM   & E ;

FIELD ADDR = [A7..A2] ;
ROMdis  = ADDR:[E0] & IORQ ;
SPDsel  = ADDR:[E4] & IORQ ;
IRQvec  = ADDR:[E8] & IORQ ;
FIRQvec = ADDR:[EC] & IORQ ;